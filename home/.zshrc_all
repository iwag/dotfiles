############ -*- Mode: shell-script; coding: euc-jp -*- ####
# ~ippei/.zshrc
#   written by ippei.kishida@materials.mbox.media.kyoto-u.ac.jp
# last modified : 2004-08-02
# 修正、改変、再配布何でも可
# cf: man zshall, zshoptions
############################################################

autoload -U zmv
# % zmv '(*).jpeg' '$1.jpg'
# % zmv '(**/)foo(*).jpeg' '$1bar$2.jpg'
# % zmv -n '(**/)foo(*).jpeg' '$1bar$2.jpg' # 実行せずパターン表示のみ
# % zmv '(*)' '${(L)1}; # 大文字→小文字
# % zmv -W '*.c.org' 'org/*.c' #「(*)」「$1」を「*」で済ませられる
alias mmv='noglob zmv -W'  # 引数のクォートも面倒なので
# % mmv *.c.org org/*.c

zstyle ':completion:*' use-compctl false # compctl形式を使用しない

autoload -U colors; colors      # ${fg[red]}形式のカラー書式を有効化

hosts=( localhost `hostname` )
umask 002
cdpath=( ~ )                    # cd のサーチパス

# for zsh-completions
typeset -U path cdpath fpath manpath  # パスの重複を省く指定
path=(/usr/local/bin(N-/) /bin(N-/) /usr/sbin(N-/) /sbin(N-/) /usr/bin(N-/) ${path})
export PATH
fpath=(/usr/local/share/zsh-completions(N-/) ${fpath})

# カレントディレクトリに候補がない場合のみ cdpath 上のディレクトリを候補
zstyle ':completion:*:cd:*' tag-order local-directories path-directories
# cf. zstyle ':completion:*:path-directories' hidden true
# cf. cdpath 上のディレクトリは補完候補から外れる

## 補完時に大小文字を区別しない
zstyle ':completion:*' matcher-list 'm:{a-z}={A-Z}'

#### history
HISTFILE="$HOME/.zhistory"      # 履歴ファイル
HISTSIZE=100000           # メモリ上に保存される $HISTFILE の最大サイズ？
SAVEHIST=1000000                  # 保存される最大履歴数

#### option, limit, bindkey
setopt extended_history         # コマンドの開始時刻と経過時間を登録
setopt hist_ignore_dups         # 直前のコマンドと同一ならば登録しない
setopt hist_ignore_all_dups     # 登録済コマンド行は古い方を削除
setopt hist_reduce_blanks # 余分な空白は詰めて登録(空白数違い登録を防ぐ)
#setopt append_history  # zsh を終了させた順にファイルに記録(デフォルト)
#setopt inc_append_history # 同上、ただしコマンドを入力した時点で記録
setopt share_history    # ヒストリの共有。(append系と異なり再読み込み不要、これを設定すれば append 系は不要)
setopt hist_no_store            # historyコマンドは登録しない
setopt hist_ignore_space # コマンド行先頭が空白の時登録しない(直後ならば呼べる)


setopt list_packed              # 補完候補リストを詰めて表示
setopt print_eight_bit          # 補完候補リストの日本語を適正表示
#setopt menu_complete  # 1回目のTAB で補完候補を挿入。表示だけの方が好き
setopt no_clobber               # 上書きリダイレクトの禁止
setopt no_unset                 # 未定義変数の使用の禁止
setopt no_hup                   # logout時にバックグラウンドジョブを kill しない
setopt no_beep                  # コマンド入力エラーでBEEPを鳴らさない

setopt extended_glob            # 拡張グロブ
setopt numeric_glob_sort        # 数字を数値と解釈して昇順ソートで出力
setopt correct                  # スペルミス補完
setopt no_checkjobs             # exit 時にバックグラウンドジョブを確認しない
#setopt ignore_eof              # C-dでlogoutしない(C-dを補完で使う人用)
setopt pushd_to_home        # 引数なしpushdで$HOMEに戻る(直前dirへは cd - で)
setopt pushd_ignore_dups        # ディレクトリスタックに重複する物は古い方を削除
#setopt pushd_silent   # pushd, popd の度にディレクトリスタックの中身を表示しない
setopt auto_cd # ディレクトリ名だけでcdする
setopt interactive_comments     # コマンド入力中のコメントを認める
#setopt rm_star_silent          # rm * で本当に良いか聞かずに実行
#setopt rm_star_wait            # rm * の時に 10秒間何もしない
#setopt chase_links             # リンク先のパスに変換してから実行。
# setopt sun_keyboard_hack      # SUNキーボードでの頻出 typo ` をカバーする

setopt auto_pushd # cd したら自動的にpushdする


setopt hist_save_nodups # ヒストリファイルに保存するとき、すでに重複したコマンドがあったら古い方を削除する
setopt hist_reduce_blanks # ヒストリに保存するときに余分なスペースを削除する

setopt auto_menu # 補完候補が複数あるときに自動的に一覧表示する


setopt print_eight_bit # 日本語ファイル名を表示可能にする

# フローコントロールを無効にする
setopt no_flow_control

# 色を使用出来るようにする
autoload -Uz colors
colors

limit   coredumpsize    0       # コアファイルを吐かないようにする

stty    erase   '^H'
stty    intr    '^C'
stty    susp    '^Z'

#### bindkey
bindkey -e    # emacs 風キーバインド(環境変数 EDITOR も反映するが、こっちが優先)
bindkey '^I'   complete-word   # complete on tab, leave expansion to _expand

bindkey '^P' history-beginning-search-backward # 先頭マッチのヒストリサーチ
bindkey '^N' history-beginning-search-forward # 先頭マッチのヒストリサーチ

# run-help が呼ばれた時、zsh の内部コマンドの場合は該当する zsh のマニュアル表示
[ -n "`alias run-help`" ] && unalias run-help
autoload run-help

#### completion
#_cache_hosts=(`perl -ne  'if (/^([a-zA-Z0-9.-]+)/) { print "$1\n";}' ~/.ssh/known_hosts`)
# ↑(_cache_hosts) ~/.ssh/known_hosts から自動的に取得する
_cache_hosts=($HOST localhost)
autoload -U compinit; compinit -u


# for byobu tmux
if [ $TERM = "screen" ] || [ $SCREEN = 1 ]; then
	stty erase "^?"
fi

############################################################
## alias & function

#### PAGER
alias less="$PAGER"
alias m="$PAGER"
alias -g L="| $PAGER"
alias -g M="| $PAGER"
alias les="less"        # for typo

#### ps
if [ $ARCHI = "irix" ]; then
  alias psa='ps -ef'
else;
  alias psa='ps auxw'
fi
function pst() {                # CPU 使用率の高い方から8つ
  psa | head -n 1
  psa | sort -r -n +2 | grep -v "ps -auxww" | grep -v grep | head -n 8
}
function psm() {                # メモリ占有率の高い方から8つ
  psa | head -n 1
  psa | sort -r -n +3 | grep -v "ps -auxww" | grep -v grep | head -n 8
}
function psg() {
  psa | head -n 1
  psa | grep $* | grep -v "ps -auxww" | grep -v grep
}

#### ls
#### dircolor
if [ $ARCHI = "linux" ]; then
  alias ls="ls -F --color=auto --show-control-char"
  alias lscolor='ls -F --color=always --show-control-char'
  # 補完リストをカラー化
  zstyle ':completion:*:default' list-colors ${(s.:.)LS_COLORS}
else
fi
alias kls='ls'
alias qls='ls' # Shift-PageUp を抜けるのについ q を打ってしまうのを回避
alias sl='ls'
alias s='ls'
alias lf='ls'
alias l='ls'
alias la='ls -a'
alias ll='ls -al'

#### command
alias du='du -k'
alias mv='mv -iv'
#alias  memo    'skkfep -viesc -e jvim3 ~/memo.txt'
alias w3m='w3m -X'
alias xinit='ssh-agent xinit'
alias bell="echo '\a'"
alias view="vim -R"

# short name
alias h='head'
alias t='tail'
alias g='grep'
alias c='cat'
alias j='jobs'
#alias w='wc'   w は w のまま使う。

## global alias
alias -g H='| head'
alias -g T='| tail'
alias -g G='| grep'
alias -g C='| cat -n'
alias -g W='| wc'
#alias -g S='| sed'
#alias -g A='| awk'
alias -g ....='../..'

function cd() { builtin cd $@ && ls; }
alias cd..='cd ..'
alias cd../='cd ..'

function hgrep() { history | grep $@ }
function grep { command grep --color=tty "$@" }

# git
alias git-ammend="git commit --amend"
alias git-ca="git commit -a "
function git-propen2() { # by laiso http://qiita.com/laiso/items/8a30e3656c980863ccfa
	local current_branch_name=$(git symbolic-ref --short HEAD | xargs perl -MURI::Escape -e 'print uri_escape($ARGV[0]);')
	git config --get remote.origin.url | sed -e "s/^.*[:\/]\(.*\/.*\).git$/https:\/\/github.com\/\1\//" | sed -e "s/$/pull\/${current_branch_name}/" | xargs open
}


# doccker shortcut
function dola { command docker ps -l -q }
alias doc="docker "
alias dops="docker ps"
alias dorun="docker run"
alias dostop="docker stop"
alias doimage="docker images"
function docker_host { echo "tcp://$(/usr/local/bin/boot2docker ip 2>/dev/null):2375" }

function jsurl { curl -H "application/json" $@ }

function rm() {
  if [ -d ~/.trash ]; then
    local DATE=`date "+%y%m%d-%H%M%S"`
    mkdir ~/.trash/$DATE
    for i in $@; do
      # 対象が ~/.trash/ 以下なファイルならば /bin/rm を呼び出したいな
      if [ -e $i ]; then
        mv $i ~/.trash/$DATE/
      else
        echo "$i : not found"
      fi
    done
  else
    /bin/rm -i $@
  fi
}

#### time
REPORTTIME=20
TIMEFMT="\
    The name of this job.             :%J
    CPU seconds spent in user mode.   :%U
    CPU seconds spent in kernel mode. :%S
    Elapsed time in seconds.          :%E
    The  CPU percentage.              :%P"

# zaw
ZAW_EDITOR=vim
source ~/.zsh/zaw/zaw.zsh

[ -n "`alias history`" ] && unalias history # for ohmyzsh

# bindkey '^[r' zaw-history
bindkey '^[o' zaw-git-files
bindkey '^[e' zaw-open-file

function peco-select-history() {
	local tac
	if which tac > /dev/null; then
		tac="tac"
	else
		tac="tail -r"
	fi
	BUFFER=$(history -n 1 | \
		eval $tac | \
		peco --query "$LBUFFER")
	CURSOR=$#BUFFER
	#zle clear-screen
}
zle -N peco-select-history
bindkey '^[r' peco-select-history

function agp() {
	local tac
	if which tac > /dev/null; then
		tac="tac"
	else
		tac="tail -r"
	fi
	FILE=$(ag $@ | peco |  awk -F':' '{print $1}')
	
	$ZAW_EDITOR $FILE
}

function agv() {
  vim $(ag $@ | peco --query "$LBUFFER" | awk -F : '{print "-c " $2 " " $1}')
}

# config go and ghq
# go get github.com/motemen/ghq
# git config --global ghq.root ~/Devel/src
export GOROOT=`go env GOROOT`
export GOPATH="${HOME}/Devel/go"
export PATH="${PATH}:${GOROOT}/bin:${GOPATH}/bin"

if which pry > /dev/null  ; then
	# nothing
else
	alias pry="/usr/local/lib/ruby/gems/2.0.0/gems/pry-0.10.0/bin/pry"
fi

if [ -e ~/.zshrc_private ]; then
  source ~/.zshrc_private
fi
